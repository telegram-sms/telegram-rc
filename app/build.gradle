apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    //noinspection GradleDependency
    compileSdk 34
    //noinspection GrDeprecatedAPIUsage
    defaultConfig {
        applicationId "com.qwe7002.telegram_rc"
        minSdkVersion 29
        //noinspection OldTargetApi
        targetSdkVersion 34
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "debug-do_not_leak" : System.getenv("VERSION_NAME")
    }
    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }
    buildFeatures {
        buildConfig true
        aidl true
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    signingConfigs {
        release {
            storeFile file("keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled true
            v2SigningEnabled true 
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
        debug {
            minifyEnabled false
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }
    namespace 'com.qwe7002.telegram_rc'
}



dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.13.0'
    //noinspection GradleDependency
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.squareup.okio:okio:3.16.2'
    implementation "com.squareup.okhttp3:okhttp:5.2.1"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:5.2.1"
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation 'aga.android:luch:0.4'
    implementation 'com.linkedin.dexmaker:dexmaker:2.28.6'
    //noinspection GradleDependency
    implementation "androidx.core:core-ktx:1.13.1"
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:2.2.20'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0'
    implementation 'com.tencent:mmkv:2.2.4'
    //Shizuku
    implementation("dev.rikka.shizuku:api:13.1.5")
    implementation("dev.rikka.shizuku:provider:13.1.5")
    implementation 'org.lsposed.hiddenapibypass:hiddenapibypass:6.1'
    
    // Room database dependencies
    implementation "androidx.room:room-runtime:2.8.2"
    implementation "androidx.room:room-ktx:2.8.2"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    //noinspection KaptUsageInsteadOfKsp
    kapt "androidx.room:room-compiler:2.8.2"
}
repositories {
    google()
    mavenCentral()
}

tasks.register('copy_language_pack', Copy) {
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}