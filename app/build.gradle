apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.qwe7002.telegram_rc"
        minSdkVersion 26
        //noinspection ExpiredTargetSdkVersion,OldTargetApi
        targetSdkVersion 29
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "debug-do_not_leak" : System.getenv("VERSION_NAME")
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11

    }
    kotlinOptions {
        jvmTarget = '11'
    }
    signingConfigs {
        release {
            storeFile file("keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled true
            v2SigningEnabled true 
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
        debug {
            minifyEnabled false
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }
    namespace 'com.qwe7002.telegram_rc'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.browser:browser:1.5.0'
    implementation 'com.squareup.okio:okio:3.2.0'
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.2"
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'io.github.pilgr:paperdb:2.7.2'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation 'org.altbeacon:android-beacon-library:2.19.5'
    implementation 'com.linkedin.dexmaker:dexmaker:2.28.1'
    implementation "androidx.core:core-ktx:1.10.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.10"
}
repositories {
    mavenCentral()
}

tasks.register('copy_language_pack', Copy) {
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}
